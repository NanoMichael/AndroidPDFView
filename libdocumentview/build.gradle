apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 26
        versionCode 11
        versionName "1.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "com.github.barteksc:pdfium-android:1.8.2"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = 'io.ea'
            pom.artifactId = 'pdfview'
            pom.version = android.defaultConfig.versionName
            def uploadUrl = properties.getProperty('archive.url.private')
            def user = properties.getProperty('archive.user.private')
            def pwd = properties.getProperty('archive.pwd.private')
            repository(url: uploadUrl) {
                authentication(userName: user, password: pwd)
            }
        }
    }
}
